plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.liquibase.gradle' version '2.0.2'
	id 'groovy'
}

group = 'com.dalolorn.croztest'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.codehaus.groovy:groovy'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	liquibaseRuntime 'org.slf4j:slf4j-api:1.7.30'
	liquibaseRuntime 'org.slf4j:slf4j-simple:1.7.30'
	liquibaseRuntime 'ch.qos.logback:logback-core:1.2.3'
	liquibaseRuntime 'org.liquibase:liquibase-core:3.8.1'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2:1.4.200'
}

test {
	useJUnitPlatform()
}

project.ext.mainUrl = 'jdbc:postgresql://localhost:5432/jokemanager'
project.ext.mainUsername = 'jokeadmin'
project.ext.mainPassword = 'jokeadminpw'
project.ext.runList = 'runMainSQL'

liquibase {
	activities {
		runMain {
			changeLogFile 'src/main/db/main.groovy'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
		}
		diffMain {
			changeLogFile 'src/main/db/main.groovy'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
			difftypes 'data'
		}
		// I'm gonna define new activities for the SQL file instead of deleting the ones for the Groovy file.
		runMainSQL {
			changeLogFile 'src/main/db/data.sql'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
		}
		diffMainSQL {
			changeLogFile 'src/main/db/data.sql'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
			diffTypes 'data'
		}
	}
	runList = project.ext.runList
}
